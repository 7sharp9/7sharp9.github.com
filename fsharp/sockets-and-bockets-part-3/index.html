<!doctype html><html><head><title>Sockets and Bockets 3 &#183; 7sharp9</title><meta charset=utf-8><meta name=viewport content="width=device-width,initial-scale=1,maximum-scale=1"><meta name=generator content="Hugo 0.82.1"><link rel=stylesheet href=https://7sharp9.dev/css/vec.css><link rel=apple-touch-icon-precomposed sizes=144x144 href=/apple-touch-icon-144-precomposed.png><link rel="shortcut icon" href=/favicon.ico><link href rel=alternate type=application/rss+xml title=7sharp9></head><body><header><nav><ul><li class=pull-left><a href=https://7sharp9.dev/>/Home</a></li><li class=pull-left><a href=/about>/About</a></li><li class=pull-left><a href=/fsharp>/F#</a></li><li class=pull-left><a href=/running>/Running</a></li><li class=pull-left><a href=/elm>/Elm</a></li><li class=pull-right><a href><i class="fas fa-rss"></i></a></li></ul></nav></header><div class="content wide"><section class=post><h1 class=post-title><a href=https://7sharp9.dev/fsharp/sockets-and-bockets-part-3/>Sockets and Bockets 3</a></h1><span class=post-date>Jan 20, 2011</span><div class=post-content><h2 id=welcome-to-part-three>Welcome to part three!</h2><p>As promised heres a description of the inner workings.  I&rsquo;m sick to death of
typing SocketAsyncEventArgs so from now on I will refer to it as SAEA.</p><p><strong>BocketPool</strong><br>The BocketPool has an interesting name and with it an interesting constructor!
It takes the following parameters:</p><p><strong>number</strong>: The number of items to create in the BocketPool. <strong>size</strong>: The size of each buffer in bytes. <strong>callback</strong>: A callback function which is invoked whenever the SAEA object completes its operation.</p><pre><code>type BocketPool( number, size, callback) as this =
    let number = number
    let size = size
    let totalsize = (number * size)
    let buffer = Array.create totalsize 0uy
    let pool = new BlockingCollection&lt;SocketAsyncEventArgs&gt;(number:int)
</code></pre><p>A <strong>buffer</strong> is created with a size equal to the (<strong>number</strong> * <strong>size</strong>)
in bytes.</p><pre><code>do
      let rec loop n =
        match n with
        | x when x &lt; totalsize -&gt;
          let saea = new SocketAsyncEventArgs()
          saea.Completed |&gt; Observable.add callback
          saea.SetBuffer(buffer, n, size)
          this.CheckIn(saea)
          loop (n + size)
        | _ -&gt; ()
      loop 0
</code></pre><p>The tail recursive loop function creates a SAEA object and adds it to the
BlockingCollection(pool).</p><p>The buffer is assigned to each SAEA but each is given a unique offset to use,
this is done by the SetBuffer method.  Using this method of allocation, memory
fragmentation is reduced to a minimum by allowing the same buffer to be
reused.</p><p>We use the pipeline operator to attach the <strong>Completed</strong> event to the callback
method that is passed in the constructor.</p><p>The CheckIn, CheckOut, and Count methods are simply wrappers around the
BlockingCollection.</p><p>We also implement <strong>IDisposable</strong> to take care of the disposal of the SAEA in the
BlockingCollection.</p><p><strong>Connection</strong><br>The main purpose for this type is to encapsulate the sending and receiving of
messages for a particular client. A BocketPool is created for both the send
and receive operations, the receiveCompleted and SentCompleted are invoked
when the respective operations complete.</p><p><strong>Send</strong></p><pre><code>member this.Send (msg:byte[]) =
  let s = sendPool.CheckOut()
  Buffer.BlockCopy(msg, 0, s.Buffer, s.Offset, msg.Length)
  socket.SendAsyncSafe(this.sendCompleted, s)
</code></pre><p>Initially a Bocket is checked out of the sendPool using sendPool.Checkout(),
the <strong>msg</strong> byte array is copied to the corresponding <strong>Offset</strong> property of
the SAEA.</p><p>Finally the SendAsyncSafe extension method is called passing in the SAEA and
the callback.</p><p><strong>sendCompleted</strong></p><pre><code>member this.sendCompleted (args: SocketAsyncEventArgs) =
      try
        match args.LastOperation with
        | SocketAsyncOperation.Send -&gt;
          match args.SocketError with
          | SocketError.Success -&gt;
            ()
          | SocketError.NoBufferSpaceAvailable
          | SocketError.IOPending
          | SocketError.WouldBlock -&gt;
            if not(anyErrors) then
              anyErrors &lt;- true
              failwith &quot;Buffer overflow or send buffer timeout&quot;
          | _ -&gt; args.SocketError.ToString() |&gt; printfn &quot;socket error on send: %s&quot;
        | _ -&gt; failwith &quot;invalid operation, should be receive&quot;
      finally
        sendPool.CheckIn(args)
</code></pre><p>This function matches the LastOperation property of the SAEA using pattern
matching, this ensures that the LastOperation is always SocketError.Success.</p><p>We raise exceptions on NoBufferSpaceAvailable, IOPending, and WouldBlock as
buffer overflows and match any other conditions the wildcard.</p><p>Finally we Check the Bocket back in so that it can be reused.</p><p><strong>receiveCompleted</strong></p><pre><code>member this.receiveCompleted (args: SocketAsyncEventArgs) =
      try
        match args.LastOperation with
        | SocketAsyncOperation.Receive -&gt;
          match args.SocketError with
          | SocketError.Success -&gt;
            socket.ReceiveAsyncSafe( this.receiveCompleted, receivePool.CheckOut())
            let data = Array.create args.BytesTransferred 0uy
            Buffer.BlockCopy(args.Buffer, args.Offset, data, 0, data.Length)
            let client = args.RemoteEndPoint
            args.RemoteEndPoint &lt;- null
            data |&gt; printfn &quot;received data: %A&quot;
          | _ -&gt; args.SocketError.ToString() |&gt; printfn &quot;socket error on receive: %s&quot;
        | _ -&gt; failwith &quot;unknown operation, should be receive&quot;
      finally
        receivePool.CheckIn(args)
</code></pre><p>This function is very similar to the sendCompleted and could probably be
refactored a bit using the [Hole in the middle
pattern](<a href=http://enfranchisedmind.com/blog/posts/the-hole-in-the-middle->http://enfranchisedmind.com/blog/posts/the-hole-in-the-middle-</a>
pattern/).  Again we check to ensure the last operation was a success, we
checkout another Bocket and start another ReceiveAsyncSafe. This ensures that
the socket can begin another receive operation as soon as possible while we
take the data from the SAEA Buffer, we do this with Buffer.Block copy.</p><p>If this were a fully-fledged API then we would raise an event here so that
users of the component could consume the data.</p><p>In my own component the data is inserted into a series of processing stages
using the [Pipeline Pattern](<a href=http://www.cise.ufl.edu/research/ParallelPatterns>http://www.cise.ufl.edu/research/ParallelPatterns</a>
/PatternLanguage/AlgorithmStructure/Pipeline.htm), which I will be may
describe in a future post if anyone&rsquo;s interested.</p><p><strong>TcpListener</strong><br>The TcpListener is very similar to the Connection object in that it has a pool
of SAEA objects that are used to accept connection from clients, again a round
of refactoring could be done here to avoid duplication with the Connection
type.  The main difference is that we don&rsquo;t need to use the Buffer on the SAEA
to send anything to the client when it initially connects.</p><p><strong>acceptCompleted</strong></p><pre><code>member this.acceptcompleted (args : SocketAsyncEventArgs) =
      try
        match args.LastOperation with
        | SocketAsyncOperation.Accept -&gt;
          match args.SocketError with
          | SocketError.Success -&gt;
            listeningSocket.AcceptAsyncSafe( this.acceptcompleted, acceptPool.Take())
            //create new connection
            let connection = newConnection args.AcceptSocket
            connection.Start()  
            //update stats
            reportConnections   
            //async start of messages to client
            startSending connection  
            //remove the AcceptSocket because we will be reusing args
            args.AcceptSocket &lt;- null
          | _ -&gt; args.SocketError.ToString() |&gt; printfn &quot;socket error on accept: %s&quot;
        | _ -&gt; args.LastOperation |&gt; failwith &quot;Unknown operation, should be accept but was %a&quot;
      finally
        acceptPool.Add(args)
</code></pre><p>This function is similar to the send and receive completed methods in the
Connection type, although this time we create a Connection object and call the
Start function, this puts the Connection into receive mode.</p><p>The reportConnections is called next which simply prints how many clients are
connected, we now start an Asyncronous workflow using the startSending
function.</p><p>Finally we set the AcceptSocket property to null on the SAEA object and add it
back to the BlockingCollection so that it can be reused.</p><p>The purpose of the BlockingCollection here is to have a fixed pool of SAEA
that block when there isn&rsquo;t an SAEA to service the new connection, this could
be a potential issue for the client as it could timeout while waiting for a
connection but this is a far preferable situation than causing your server to
be effectively denied service due to overload.</p><p><strong>startSending</strong></p><pre><code>let startSending connection =
      Async.Start (async {
        try
          use _holder = connection
          do! asyncServiceClient connection
        with e -&gt;
          if not(anyErrors) then
            anyErrors &lt;- true
            Console.WriteLine(&quot;server ERROR&quot;)
          raise e
        } )
</code></pre><p>This function uses the syntactic sugar of the asynchronous workflows to start
an operation on the Thread pool, once queued on the thread pool it is wrapped
in a using block with the <strong>_use <em>holder = connection</em></strong> statement and
asynchronously calls the asyncServiceClient function, this has the effect of
disposing of the Connection type when it exits scope or encounters an
exception.</p><p><strong>asyncServiceClient</strong></p><pre><code>let asyncServiceClient (client: Connection) = async {
      client.Send(header)
      while true do
        do! asyncWriteStockQuote(client) }
</code></pre><p>This function sends a one byte header message to the client using the
Connection.Send, followed by calling asyncWriteStockQuote in a continuous
loop.</p><p><strong>asyncWriteStockQuote</strong></p><pre><code>let asyncWriteStockQuote(connection:Connection) = async {
      do! Async.Sleep 1000
      connection.Send(testMessage)
      Interlocked.Increment(&amp;numWritten) |&gt; ignore }
</code></pre><p>This function sleeps for 1000ms and uses the Connection.Send function to sent
a message to the client, the number of results is updated using the
Interlocked class.</p><p>I would like to refer you to <a href=http://lorgonblog.wordpress.com/2010/03/28/f-async-on-the-server-side/>Brian McNamara&rsquo;s
post</a>
that describes this part in more detail.  The only difference in our workflow
is that we don&rsquo;t use a stream operation as we have the SendAsyncSafe function
to do all the work for us.  IDispose is also implemented on this type too as
we have to dispose of the SAEA objects that are used for the asynchronous
accepts.</p><p><strong>createTcpSocket</strong></p><pre><code>let createTcpSocket() =
      new Socket(AddressFamily.InterNetwork, SocketType.Stream, ProtocolType.Tcp)
</code></pre><p>This function simply wraps the Sockets class constructor mapping it to: Tcp
protocol, Streaming, and InterNetwork Address type.</p><p><strong>createListener</strong></p><pre><code>let createListener (ip:IPAddress, port, backlog) =
      let s = createTcpSocket()
      s.Bind(new IPEndPoint(ip, port))
      s.Listen(backlog); s
</code></pre><p>This function calls the createTcpSocket function, binds to the IPAddress and
port that are passed in and starts listening for connections.</p><p><strong>Start</strong></p><pre><code>member this.start () =
      listeningSocket.AcceptAsyncSafe( this.acceptcompleted, acceptPool.Take())
      while true do
      Thread.Sleep 1000
      let count = Interlocked.Exchange(&amp;numWritten, 0)
      count |&gt; printfn &quot;Quotes per sec: %A&quot;
</code></pre><p>This function starts the whole process of listening for a connection from
clients.  A SAEA is taken from the BlockingCollection and AcceptAsyncSafe is
called.</p><p>I have tried to describe all of the functions that I think merit a description
but I have been involved in this sort of code for years now so if you have any
queries feel free to just drop a comment and I will try to help.</p><p>When looking through the code remember that this is just a demo, I am
currently still working on a few things but may offer the full API available
for download at a later date or put it on GitHub.</p><p>In part four we are going to compare some of the differences in operation
between the xxxAsync and the IAsync pattern, obviously there is a lot more
code and inherent complexity in this implementation but in high volume
situations it makes a lot of difference.</p><p>See you next time.</p></div></section><section class="pagination clearfix"><a class="btn previous" href=https://7sharp9.dev/fsharp/sockets-and-bockets-part-2/>Sockets and Bockets 2</a>
<a class="btn next" href=https://7sharp9.dev/fsharp/sockets-and-bockets-part-4/>Sockets and Bockets 4</a></section><section id=disqus_thread class=disqus></section><script>var disqus_config=function(){this.page.url="https://7sharp9.dev/fsharp/sockets-and-bockets-part-3/"};(function(){var a=document,b=a.createElement('script');b.src='//7sharp9.disqus.com/embed.js',b.setAttribute('data-timestamp',+new Date),(a.head||a.body).appendChild(b)})()</script><noscript>Please enable JavaScript to view the <a href=https://disqus.com/?ref_noscript rel=nofollow>comments powered by Disqus.</a></noscript></div><footer><div class="footer-info pull-right">© 2021 Dave Thomas</div></footer><script src=https://7sharp9.dev/js/highlight.min.js></script><script>hljs.initHighlightingOnLoad()</script><script>(function(a,e,f,g,b,c,d){a.GoogleAnalyticsObject=b,a[b]=a[b]||function(){(a[b].q=a[b].q||[]).push(arguments)},a[b].l=1*new Date,c=e.createElement(f),d=e.getElementsByTagName(f)[0],c.async=1,c.src=g,d.parentNode.insertBefore(c,d)})(window,document,'script','//www.google-analytics.com/analytics.js','ga'),ga('create','UA-10152365-8','auto'),ga('send','pageview')</script></body></html>