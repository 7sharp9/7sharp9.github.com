<!doctype html><html><head><title>F# Dataflow Agents Part I &#183; 7sharp9</title><meta charset=utf-8><meta name=viewport content="width=device-width,initial-scale=1,maximum-scale=1"><meta name=generator content="Hugo 0.82.1"><link rel=stylesheet href=https://7sharp9.dev/css/vec.css><link rel=apple-touch-icon-precomposed sizes=144x144 href=/apple-touch-icon-144-precomposed.png><link rel="shortcut icon" href=/favicon.ico><link href rel=alternate type=application/rss+xml title=7sharp9></head><body><header><nav><ul><li class=pull-left><a href=https://7sharp9.dev/>/Home</a></li><li class=pull-left><a href=/about>/About</a></li><li class=pull-left><a href=/fsharp>/F#</a></li><li class=pull-left><a href=/running>/Running</a></li><li class=pull-left><a href=/elm>/Elm</a></li><li class=pull-right><a href><i class="fas fa-rss"></i></a></li></ul></nav></header><div class="content wide"><section class=post><h1 class=post-title><a href=https://7sharp9.dev/fsharp/fsharp-dataflow-agents-i/>F# Dataflow Agents Part I</a></h1><span class=post-date>Jan 22, 2012</span><div class=post-content><p>This is going to be a new series on using TPL Dataflow with F#. First a little bit of history and background.</p><h2 id=tpl-dataflows-heritage-and-background>TPL Dataflows heritage and background</h2><p>TPL Dataflow or <a href=http://msdn.microsoft.com/en-us/devlabs/gg585582>(TDF)</a> has been around for quite a
while, it first surfaced more than a year ago as the successor to the Concurrency and Coordination Runtime
<a href=http://msdn.microsoft.com/en-us/library/bb648752.aspx>(CCR)</a> and with coming release of .Net 4.5 it will
be part of the <code>System.Threading.Tasks.Dataflow</code> namespace. Elements of the now halted project
<a href=http://msdn.microsoft.com/en-us/devlabs/dd795202>Axum</a> are also present within the design of TDF.</p><h3 id=concurrency-and-coordination-runtime-ccr>Concurrency and Coordination Runtime (CCR)</h3><p>CCR is a library that deals with asynchrony, concurrency, and coordination between blocks of asynchronous
code so that the programmer doesn&rsquo;t have to. All of the low level details of synchronization and error
propagation are taken care of in a consistent fashion. CCR is still is included in
<a href=ttp://www.microsoft.com/robotics/>Microsoft Robotics Studio</a> where it is used extensively to exploit
parallel hardware and deal with partial failure of systems.</p><h3 id=axum>Axum</h3><p>Axum was another interesting Microsoft research project, it also utilized the actor model embracing the
principles of isolation, and message-passing. There was also extensive use symbolic operators as a terse
short hand way to indicate operations between actors. For example <code>&lt;--</code> defined a way to pass a message
to an actor. Theres was also a similarity to CCR as Axum used the concepts of Ports and channels in a similar
way. It was a very interesting project and it was a shame it was put on hold.</p><h3 id=tpl-dataflow-tdf>TPL Dataflow (TDF)</h3><p>TDF builds on CCR and Axum, consolidating and refine to produce a more friendly fluent interface, much in
the same vain as Language-Integrated Query <a href=http://msdn.microsoft.com/en-us/library/bb308959.aspx>(LINQ)</a>
and Reactive Extensions <a href=http://msdn.microsoft.com/en-us/data/gg577609>(RX)</a>.</p><p>TDF is built around a number of different blocks which can be combined or linked together. There are three
different categories of blocks are as follows:</p><h4 id=buffering-blocks>Buffering Blocks</h4><p>Buffering blocks simply buffer data in various ways before passing the data on to another block.</p><ul><li><strong>BufferBlock&lt;&lsquo;T></strong> - The BufferBlock act as a first-in-first-out (FIFO) queue, buffering each input.</li><li><strong>BroadcastBlock&lt;&lsquo;T></strong> - The BroadcastBlock linking to multiple targets copying the data to each of the
connected blocks.</li><li><strong>WriteOnceBlock&lt;&lsquo;T></strong> - The WriteOnceBlock acts like an immutable target, after an item first item
is passed to it, it effectively becomes read only.</li></ul><h4 id=executor-blocks>Executor Blocks</h4><p>The executor blocks run user supplied code in the form of a lambda expressions or a <code>Task&lt;'T></code>.</p><ul><li><strong>ActionBlock&lt;&lsquo;TInput></strong> - The ActionBlock acts like the <code>Action&lt;'T></code> delegate performing an action
on each datum posted to it.</li><li><strong>TransformBlock&lt;&lsquo;TInput,&lsquo;TOutput></strong> - The TransformBlock acts just like the ActionBlock except
that the action performed can have an output, this output is buffered and behaves just like a BufferBlock.</li><li><strong>TransformManyBlock&lt;&lsquo;TInput,&lsquo;TOutput></strong> - The TransformManyBlock is just like a TransformBlock except
that is can produce more than one output for a given datum.</li></ul><h4 id=joining-blocks>Joining Blocks</h4><p>The Joining Blocks Combining or join data together in different ways.</p><ul><li><strong>BatchBlock&lt;&lsquo;T></strong> - The BatchBlock Combines multiple single items together, the items are
represented by arrays of elements. The items are grouped together is batches and then passed on to
another block.</li><li><strong>JoinBlock&lt;&lsquo;T1,&lsquo;T2,…></strong> - The JoinBlock acts as a form of <code>Enumerable.Zip&lt;'T1,'T2,'TResult></code>
except the zip operation is performed on the items in the source array.</li><li><strong>BatchedJoinBlock&lt;&lsquo;T1,&lsquo;T2,…></strong> This block as the name suggests simply aggregates the JoinBlock and
the BatchBlock together.</li></ul><p>Thats an ultra high level tour thats only just scratches the surface. I recommend you check out the
<a href="http://www.microsoft.com/download/en/details.aspx?id=14782">Introduction to TPL Dataflow</a> document to read
up on the details. Theres a few more resources in the <a href=http://msdn.microsoft.com/en-us/devlabs/gg585582>DevLabs area</a>
that you might find useful. Hopefully this series should also shed a bit more light on TDF as we go along&mldr;</p><h3 id=f-asynchronous-workflows-and-agents>F# Asynchronous Workflows and Agents</h3><p>So where does that leave us in F#?<br>In F# we have <a href=http://msdn.microsoft.com/en-us/library/dd233250.aspx>Asynchronous Workflows</a> and
<a href=http://msdn.microsoft.com/en-us/library/ee370357.aspx>agents</a> and they help immensely in the concurrency
and message passing, but that doest mean that we cant take advantage of the new features and refinements
much in the same way as we can use Asynchronous Workflows to take advantage of Tasks.</p><p>This post is going to be centered around F# agents but with a twist. First of all are going to be
reimplementing a MailboxProcessor using TDF for the underlying processing. This will allow us to to use
all of our existing agent code and examples and also stay within the F# agent paradigm. Following this
approach we could also make use of the <code>DataflowBlockOptions</code> type, it has some interesting
properties which we will look at in future posts:</p><ul><li>TaskScheduler</li><li>CancellationToken</li><li>MaxMessagesPerTask</li><li>BoundedCapacity</li></ul><h3 id=implementation>Implementation</h3><p>In this post we are going replicate the MailboxProcessor, we will be using Tomas Petricek&rsquo;s caching agent
example from <a href=http://fssnip.net/8V>FSSnip</a>). I have made a couple of modification to Tomas&rsquo;s code.<br>I replaced the Dictionary type with a ConcurrentDictionary so that the caching agent could be called multiple
times successively without the dictionary throwing an exception due to it already containing a key from a
previous cached result. I also changed the example code so that it requests cached HTML from the caching
agent ten times with a 400ms interval in between each.</p><pre><code>module TplAgents
open System
open System.Collections.Generic
open System.Collections.Concurrent
open FsDataflow
open System.Net
open Microsoft.FSharp.Control.WebExtensions

type CachingMessage =
    | Add of string * string
    | Get of string * AsyncReplyChannel&lt;option&lt;string&gt;&gt;
    | Clear

let caching = DataflowAgent.Start(fun agent -&gt; async {
   let table = ConcurrentDictionary&lt;string, string&gt;()
   while true do
      let! msg = agent.Receive()
      match msg with
      | Add(url, html) -&gt; 
         // Add downloaded page to the cache
         table.AddOrUpdate(url, html, fun k v -&gt; html) |&gt; ignore
      | Get(url, repl) -&gt; 
         // Get a page from the cache - returns 
         // None if the value isn't in the cache
         if table.ContainsKey(url) then
            repl.Reply(Some table.[url])
         else
            repl.Reply(None) 
      | Clear -&gt; 
           table.Clear() })
</code></pre><pre><code>/// Prints information about the specified web site using cache
let printInfo url = async {
   // Try to get the cached HTML from the caching agent
   let! htmlOpt = caching.PostAndAsyncReply(fun ch -&gt; Get(url, ch))
   match htmlOpt with
   | None -&gt;
       // New url - download it and add it to the cache
       use wc = new WebClient()
       let! text = wc.AsyncDownloadString(Uri(url))
       caching.Post(Add(url, text))   
       Console.WriteLine( sprintf &quot;Download: %s (%d)&quot; url text.Length)
   | Some html -&gt;
       // The url was downloaded earlier 
       Console.WriteLine( sprintf &quot;Cached: %s (%d)&quot; url html.Length) }

let printfuncpro = printInfo &quot;http://functional-programming.net&quot;
// Print information about a web site -
// Run this repeatedly to use cached value
for i in 1 .. 10 do
   printfuncpro |&gt; Async.Start
   Async.RunSynchronously &lt;| Async.Sleep 400

// Clear the cache - 'printInfo' will need to
// download data from the web site again
Console.WriteLine(sprintf &quot;Clearing the cache&quot;)
caching.Post(Clear)
printfuncpro |&gt; Async.Start

Console.ReadKey() |&gt; ignore
</code></pre><p>Looking at the implementation above you can see that we need to implement the following members:</p><ul><li>Start:<code>unit -> unit</code></li><li>Receive:<code>?int -> Async&lt;'Msg></code></li><li>Post:<code>'Msg -> unit</code></li><li>PostAndTryAsyncReply:<code>(AsyncReplyChannel&lt;'Reply> -> 'Msg) * ?int -> Async&lt;'Reply option></code></li><li>PostAndAsyncReply:<code>(AsyncReplyChannel&lt;'Reply> -> 'Msg) * int option -> Async&lt;'Reply></code></li><li>static member Start:<code>(MailboxProcessor&lt;'Msg> -> Async&lt;unit>) * ?CancellationToken -> MailboxProcessor&lt;'Msg></code></li></ul><p>These are the only members we need to complete the caching agent example, I didn&rsquo;t want bamboozle everyone
with an explosion of code from the onset so the remaining members will be implemented as and when we need
them. When we have implemented all the members from MailboxProcessor Ill post the full source on my
<a href=https://github.com/7sharp9>GitHub</a> account.</p><p>The following members will be outstanding but it should be fairly trivial to implement them
once we have completed the code here.</p><ul><li>PostAndReply:<code>(AsyncReplyChannel&lt;'Reply> -> 'Msg) * int option -> 'Reply</code></li><li>Scan:<code>('Msg -> Async&lt;'T> option) * ?int -> Async&lt;'T></code></li><li>TryPostAndReply:<code>(AsyncReplyChannel&lt;'Reply> -> 'Msg) * ?int -> 'Reply option</code></li><li>TryReceive:<code>?int -> Async&lt;'Msg option></code></li><li>TryScan:<code>('Msg -> Async&lt;'T> option) * ?int -> Async&lt;'T option></code></li><li>CurrentQueueLength:<code>int</code></li><li>DefaultTimeout:<code>int with get, set</code></li></ul><p>So here we go, this is the Dataflow implementation of the MailboxProcessor:</p><pre><code>module FsDataflow
open System
open System.Threading
open System.Threading.Tasks
open System.Threading.Tasks.Dataflow
open System.Collections.Concurrent

type DataflowAgent&lt;'Msg&gt;(initial, ?cancellationToken) =
    let cancellationToken = 
        defaultArg cancellationToken Async.DefaultCancellationToken
    let mutable started = false
    let errorEvent = new Event&lt;System.Exception&gt;()
    let incomingMessages = new BufferBlock&lt;'Msg&gt;()
    let mutable defaultTimeout = Timeout.Infinite
    
    [&lt;CLIEvent&gt;]
    member this.Error = errorEvent.Publish

    member this.Start() =
        if started 
            then raise (new InvalidOperationException(&quot;Already Started.&quot;))
        else
            started &lt;- true
            let comp = async { try do! initial this 
                               with error -&gt; errorEvent.Trigger error }
            Async.Start(computation = comp, cancellationToken = cancellationToken)

    member this.Receive(?timeout) = 
        Async.AwaitTask &lt;| incomingMessages.ReceiveAsync()

    member this.Post(item) = 
        let posted = incomingMessages.Post(item)
        if not posted then
            raise (InvalidOperationException(&quot;Incoming message buffer full.&quot;))

    member this.PostAndTryAsyncReply(replyChannelMsg, ?timeout) = 
        let timeout = defaultArg timeout defaultTimeout
        let resultCell = AsyncResultCell&lt;_&gt;()
        let msg = replyChannelMsg(AsyncReplyChannel&lt;_&gt;(fun reply -&gt; resultCell.RegisterResult(reply)))
        let posted = incomingMessages.Post(msg)
        if posted then
            match timeout with
            |   Threading.Timeout.Infinite -&gt; 
                    async { let! result = resultCell.AsyncWaitResult
                            return Some(result) }  
            |   _ -&gt;
                    async { let! ok =  resultCell.GetWaitHandle(timeout)
                            let res = (if ok then Some(resultCell.GrabResult()) else None)
                            return res }
        else async{return None}

    member this.PostAndAsyncReply( replyChannelMsg, ?timeout) =                 
            let timeout = defaultArg timeout defaultTimeout
            match timeout with
            |   Threading.Timeout.Infinite -&gt; 
                let resCell = AsyncResultCell&lt;_&gt;()
                let msg = replyChannelMsg (AsyncReplyChannel&lt;_&gt;(fun reply -&gt; resCell.RegisterResult(reply) ))
                let posted = incomingMessages.Post(msg)
                if posted then
                    resCell.AsyncWaitResult  
                else
                    raise (InvalidOperationException(&quot;Incoming message buffer full.&quot;))
            |   _ -&gt;            
                let asyncReply = this.PostAndTryAsyncReply(replyChannelMsg, timeout=timeout) 
                async { let! res = asyncReply
                        match res with 
                        | None -&gt;  return! raise (TimeoutException(&quot;PostAndAsyncReply TimedOut&quot;))
                        | Some res -&gt; return res }           
                    
    static member Start(initial, ?cancellationToken) =
        let dfa = DataflowAgent&lt;'Msg&gt;(initial, ?cancellationToken = cancellationToken)
        dfa.Start()
        dfa
</code></pre><p>The crux of the implementation from TDF&rsquo;s point of view is the use of the BufferBlock.<br>This is one of the most fundamental blocks within TDF. Its the equivalent of the <code>Port&lt;'T></code>
type from CCR and the <code>Mailbox</code> type from F# which is used internally within the
MailboxProcessor. As mentioned abouve the BufferBlock type is a first-in-first-out (FIFO) buffer
and is responsible for buffering any data that is Posted to it.</p><p>OK, I&rsquo;m going to leave it at that for now while you digest the code presented here.</p><p>In part II I will be drilling into the detail on whats going on internally and also describing more
of the TDF model, so tune in soon for Part II.</p><p>Until next time&mldr;</p></div></section><section class="pagination clearfix"><a class="btn previous" href=https://7sharp9.dev/fsharp/fixing-a-hole/>Fixing a hole...</a>
<a class="btn next" href=https://7sharp9.dev/fsharp/fsharp-dataflow-agents-ii/>F# Dataflow Agents Part II</a></section><section id=disqus_thread class=disqus></section><script>var disqus_config=function(){this.page.url="https://7sharp9.dev/fsharp/fsharp-dataflow-agents-i/"};(function(){var a=document,b=a.createElement('script');b.src='//7sharp9.disqus.com/embed.js',b.setAttribute('data-timestamp',+new Date),(a.head||a.body).appendChild(b)})()</script><noscript>Please enable JavaScript to view the <a href=https://disqus.com/?ref_noscript rel=nofollow>comments powered by Disqus.</a></noscript></div><footer><div class="footer-info pull-right">© 2021 Dave Thomas</div></footer><script src=https://7sharp9.dev/js/highlight.min.js></script><script>hljs.initHighlightingOnLoad()</script><script>(function(a,e,f,g,b,c,d){a.GoogleAnalyticsObject=b,a[b]=a[b]||function(){(a[b].q=a[b].q||[]).push(arguments)},a[b].l=1*new Date,c=e.createElement(f),d=e.getElementsByTagName(f)[0],c.async=1,c.src=g,d.parentNode.insertBefore(c,d)})(window,document,'script','//www.google-analytics.com/analytics.js','ga'),ga('create','UA-10152365-8','auto'),ga('send','pageview')</script></body></html>